buildscript {
  ext.kotlin_version = '1.9.23'
  repositories {
    google()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:8.3.2'
    classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.9'
    classpath 'com.google.gms:google-services:4.4.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath("com.spotify.ruler:ruler-gradle-plugin:2.0.0-beta-1")
    classpath "com.jraska.module.graph.assertion:plugin:2.5.0"
  }
}

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

apply plugin: "com.jraska.module.graph.assertion"

ext.moduleNameAssertAlias = "app"

moduleGraphAssert {
  allowed = [
    'app -> feature-api',
    'app -> feature',
    'app -> core-api',
    'app -> core',
    'feature -> feature-api',
    'feature -> core-api',
    'feature -> feature-api',
    'feature-api -> core-api',
    'core-api -> core-api',
    'core -> core-api',
    'core -> feature-api',
  ]
  restricted = [
    '.* -X> app',
    'feature -X> feature',
    'core -X> core',
  ]
  assertOnAnyBuild = true
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

subprojects {
  project.plugins.whenPluginAdded { plugin ->
    if ('com.android.build.gradle.AppPlugin' == plugin.class.name
      || 'com.android.build.gradle.LibraryPlugin' == plugin.class.name) {

      project.android.testOptions.unitTests.all {
        testLogging {
          events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
          exceptionFormat 'full'
        }
      }
    }
  }
}

subprojects {
  if (!gradle.startParameter.taskNames.any { it.contains("lint") }) {
    tasks.configureEach {
      if (name.startsWith("lint")) {
        enabled = false
      }
    }
  }
}

project.rootProject.subprojects.find { it.name == "core" }.afterEvaluate {
  it.tasks.named("check").configure {
    it.dependsOn(project.gradle.includedBuild("plugins").task(":test"))
  }
}

apply from: 'dependencies.gradle'
